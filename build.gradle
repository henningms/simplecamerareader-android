buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
}

group = 'com.github.henningms'
version = '1.0'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'simplecamerareader-android'
        desc = 'A wrapper around the ZXing Library for Android, making it easier to use'
        websiteUrl = 'https://github.com/henningms/simplecamerareader-android'
        issueTrackerUrl = 'https://github.com/henningms/simplecamerareader-android/issues'
        vcsUrl = 'https://github.com/henningms/simplecamerareader-android.git'
        licenses = ['Apache-2.0']
        labels = ['aar', 'android', 'zxing']
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'A wrapper around the ZXing Library for Android, making it easier to use'
                url 'https://github.com/henningms/simplecamerareader-android'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'henningms'
                        name 'Henning Mosand Stephansen'
                        email 'henningms@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/henningms/simplecamerareader-android.git'
                    developerConnection 'https://github.com/henningms/simplecamerareader-android.git'
                    url 'https://github.com/henningms/simplecamerareader-android'
                }
            }
        }
    }
}

dependencies {
    compile 'com.google.zxing:core:2.2'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
// TODO: JavaDoc doesn't compile
//    archives javadocJar
    archives sourcesJar
}
